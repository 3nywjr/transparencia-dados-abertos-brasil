PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX dbp:<http://dbpedia.org/property/>
PREFIX dbr:<http://dbpedia.org/resource/>
PREFIX ptdbp:<http://pt.dbpedia.org/property/>

SELECT ?city, ?name, ?state, ?wikidata, ?link, ?link_prefeitura, ?link_camara, ?external_link, ?link_site, ?link_site_oficial

WHERE {
    
    # select by classes
    {
        ?city a ?city_type .
        FILTER (?city_type IN (dbo:City, dbo:Settlement))
    }
    
    # select by properties
    UNION { ?city dbp:prefeito ?mayor }
    UNION { ?city dbo:wikiPageWikiLink dbr:Mayor }
    UNION { ?thing dbp:cidade ?city }
    
    # restrict query to make sure those are cities in Brazil
    FILTER (
        EXISTS { ?city dbo:wikiPageWikiLink dbr:States_of_Brazil } ||
        EXISTS { ?city ptdbp:wikiPageUsesTemplate <http://pt.dbpedia.org/resource/Predefinição:Info/Município_do_Brasil> } ||
        EXISTS { ?city dbo:country dbr:Brazil }
    )
    
    # get the properties likely to contain links
    OPTIONAL { ?city foaf:homepage ?link }
    OPTIONAL {
        ?city dbo:wikiPageExternalLink ?external_link .
        FILTER REGEX(STR(?external_link), ".gov.br")
    }
    OPTIONAL { ?city rdfs:label ?name }
    OPTIONAL { ?city dbp:estado ?state }
    OPTIONAL { ?city dbo:state/rdfs:label ?state }
    OPTIONAL {
        ?city owl:sameAs ?wikidata .
        FILTER REGEX(STR(?wikidata), "wikidata.org")
    }
    OPTIONAL { ?city ptdbp:siteCâmara ?link_camara }
    OPTIONAL { ?city ptdbp:sitePrefeitura ?link_prefeitura }
    OPTIONAL { ?city ptdbp:site ?link_site }
    OPTIONAL { ?city ptdbp:siteOficial ?link_site_oficial }
}

