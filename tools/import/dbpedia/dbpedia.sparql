PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX dbr:<http://dbpedia.org/resource/>
PREFIX dbp:<http://dbpedia.org/property/>
PREFIX foaf:<http://xmlns.com/foaf/0.1/>
PREFIX yago:<http://dbpedia.org/class/yago/>

SELECT ?city, ?name, ?state_abbr, ?state_name, ?link, ?external_link WHERE {

    # select by classes
    {
        ?city a ?city_type .
        FILTER (?city_type IN (dbo:City, dbo:Settlement))
    }

    # select by properties
    UNION { ?city dbo:wikiPageWikiLink dbr:Mayor }

    # restrict query to make sure those are cities in Brazil
    FILTER (
        EXISTS { ?city a dbr:Municipalities_of_Brazil } ||
        EXISTS { ?city dbo:wikiPageWikiLink dbr:States_of_Brazil } ||
        EXISTS { ?city dbo:country dbr:Brazil }
    )

    OPTIONAL {
        ?city foaf:homepage ?link .
    }
    OPTIONAL {
        FILTER REGEX(STR(?external_link), ".gov.br")
        ?city dbo:wikiPageExternalLink ?external_link .
    }
    OPTIONAL {
        ?city rdfs:label ?name
        FILTER(LANG(?name) = "" || LANGMATCHES(LANG(?name), "pt") || LANGMATCHES(LANG(?name), "en"))
    }
    OPTIONAL {
        ?city dbo:isPartOf ?state .
        ?state a yago:WikicatStatesOfBrazil .
        ?state dbp:coordinatesRegion ?state_abbr .
    }
    OPTIONAL {
        ?city dbo:isPartOf ?state .
        ?state a yago:WikicatStatesOfBrazil .
        ?state rdfs:label ?state_name .
        FILTER(LANG(?state_name) = "" || LANGMATCHES(LANG(?state_name), "pt"))
    }
    OPTIONAL { # cities linked to a state whose URI has changed
        ?city dbo:isPartOf ?state_old_page .
        ?state_old_page dbo:wikiPageRedirects ?state .
        ?state a yago:WikicatStatesOfBrazil .
        ?state dbp:coordinatesRegion ?state_abbr .
    }
    OPTIONAL { # cities wrongfully linked to a city instead of state
        ?city dbo:isPartOf ?other_city .
        ?other_city dbo:isPartOf ?state .
        ?state a yago:WikicatStatesOfBrazil .
        ?state dbp:coordinatesRegion ?state_abbr .
    }
}
